/*
 * Copyright 2010 Red Hat, Inc. and/or its affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.optaplanner.examples.audienciaTimeGrain.solver;
    dialect "java"

import org.optaplanner.core.api.score.buildin.hardmediumsoft.HardMediumSoftScoreHolder;

import org.optaplanner.examples.audienciaTimeGrain.domain.Audiencia;
import org.optaplanner.examples.audienciaTimeGrain.domain.AudienciaAssignment;
import org.optaplanner.examples.audienciaTimeGrain.domain.AudienciaSchedule;
import org.optaplanner.examples.audienciaTimeGrain.domain.Room;
import org.optaplanner.examples.audienciaTimeGrain.domain.TimeGrain;
import java.util.List;

global HardMediumSoftScoreHolder scoreHolder;

// ############################################################################
// Hard constraints usually
// ############################################################################

rule "Room conflict"
    when
        $leftAssignment : AudienciaAssignment(room != null, $leftId : id, $room : room)
        $rightAssignment : AudienciaAssignment(room == $room, calculateOverlap($leftAssignment) > 0, id != $leftId)
    then
        scoreHolder.penalize(kcontext, $rightAssignment.calculateOverlap($leftAssignment));
end

rule "Don't go in overtime"
    when
        AudienciaAssignment(startingTimeGrain != null, $lastTimeGrainIndex : getLastTimeGrainIndex())
        not TimeGrain(grainIndex == $lastTimeGrainIndex)
    then
        scoreHolder.penalize(kcontext, $lastTimeGrainIndex);
end

rule "Avoid Mid-break"
    when
        $left: AudienciaAssignment(startingTimeGrain != null, $lastTimeGrainIndex : getLastTimeGrainIndex(), $startTimeGrainIndex : startingTimeGrainIndex, $audiencia : audiencia);
        Audiencia(idAudiencia == $audiencia.getIdAudiencia(), $duration : numTimeGrains)
        TimeGrain(grainIndex == $startTimeGrainIndex, $firstStartingMinute : startingMinuteOfDay)
        TimeGrain(grainIndex == $lastTimeGrainIndex, startingMinuteOfDay != $firstStartingMinute + ($duration - 1) * GRAIN_LENGTH_IN_MINUTES)
    then
        scoreHolder.penalize(kcontext);
end

rule "Start and end on same day"
    when
        AudienciaAssignment(startingTimeGrain != null, $firstTimeGrain : startingTimeGrain, $lastTimeGrainIndex : getLastTimeGrainIndex())
        $lastTimeGrain : TimeGrain(grainIndex == $lastTimeGrainIndex, $firstTimeGrain.getDay() != getDay())
    then
        scoreHolder.penalize(kcontext);
end

rule "Do not conflict Juez"
       when
           $leftAssignment : AudienciaAssignment(startingTimeGrain != null, $Juez : getJuez(), $id : id)
           AudienciaAssignment(id != $id, calculateOverlap($leftAssignment) > 0, getJuez() == $Juez)
       then
           scoreHolder.penalize(kcontext);
end

rule "Do not conflict Defensor"
    when
        $leftAssignment : AudienciaAssignment(startingTimeGrain != null, audiencia != null, $Defensor : getDefensor(), $id : id)
        AudienciaAssignment(id != $id, calculateOverlap($leftAssignment) > 0, audiencia != null, getDefensor()== $Defensor)
    then
        scoreHolder.penalize(kcontext);
end

rule "Do not conflict Fiscal"
    when
        $leftAssignment : AudienciaAssignment(startingTimeGrain != null, $Fiscal : getFiscal(), $id : id)
        AudienciaAssignment(id != $id, calculateOverlap($leftAssignment) > 0, getFiscal() == $Fiscal)
    then
        scoreHolder.penalize(kcontext);
end

rule "Do not use room in prohibited time"
    when
        $timeGrain : TimeGrain(grainIndex != null)
        AudienciaAssignment(audiencia != null, room!= null, startingTimeGrain != null, timeGrainRoomRestriction($timeGrain) == true)
    then
        scoreHolder.penalize(kcontext);
end

rule "Respect Locations"
    when
        AudienciaAssignment(room != null, audiencia.ubicacion != room.ubicacion)
    then
        scoreHolder.penalize(kcontext);
end

rule "Respect Minimum Starting Time"
    when
        $left : AudienciaAssignment(startingTimeGrain != null, isMinimumStartingTime() > 0)
    then
        scoreHolder.penalize(kcontext, $left.isMinimumStartingTime());
end

rule "Respect Maximum Starting Time"
    when
        $left : AudienciaAssignment(startingTimeGrain != null, isMaximumStartingTime() > 0)
    then
        scoreHolder.penalize(kcontext, $left.isMinimumStartingTime());
end

// ############################################################################
// Soft constraints usually
// ############################################################################

rule "Do all meetings as soon as possible" salience 10
    when
        AudienciaAssignment(startingTimeGrain != null, $lastTimeGrainIndex : lastTimeGrainIndex)
    then
        scoreHolder.penalize(kcontext, $lastTimeGrainIndex);
end

rule "One TimeGrain break between two consecutive meetings"
    when
        $left : AudienciaAssignment(startingTimeGrain != null,  $leftEnd : getLastTimeGrainIndex(), $leftFirstTimeGrain : startingTimeGrain, $room : room)
        AudienciaAssignment(startingTimeGrain != null, $leftEnd == startingTimeGrain.getGrainIndex() - 1, $rightFirstTimeGrainIndex : startingTimeGrain.grainIndex, $rightFirstTimeGrain : startingTimeGrain, room == $room, isThereABreak($left) == false)
        TimeGrain(grainIndex == $rightFirstTimeGrainIndex, $leftFirstTimeGrain.day == $rightFirstTimeGrain.day)
    then
        scoreHolder.penalize(kcontext);
end

rule "One TimeGrain Juez"
    when
        $left : AudienciaAssignment(startingTimeGrain != null,  $leftEnd : getLastTimeGrainIndex(), $leftFirstTimeGrain : startingTimeGrain, $juez : getJuez())
        AudienciaAssignment(startingTimeGrain != null, $leftEnd == startingTimeGrain.getGrainIndex() - 1, $rightFirstTimeGrainIndex : startingTimeGrain.grainIndex, $rightFirstTimeGrain : startingTimeGrain, getJuez() == $juez, isThereABreak($left) == false)
        TimeGrain(grainIndex == $rightFirstTimeGrainIndex, $leftFirstTimeGrain.day == $rightFirstTimeGrain.day)
    then
        scoreHolder.penalize(kcontext);
end

rule "One TimeGrain Defensor"
    when
        $left : AudienciaAssignment(startingTimeGrain != null,  $leftEnd : getLastTimeGrainIndex(), $leftFirstTimeGrain : startingTimeGrain, $defensor : getDefensor())
        AudienciaAssignment(startingTimeGrain != null, $leftEnd == startingTimeGrain.getGrainIndex() - 1, $rightFirstTimeGrainIndex : startingTimeGrain.grainIndex, $rightFirstTimeGrain : startingTimeGrain, getDefensor() == $defensor, isThereABreak($left) == false)
        TimeGrain(grainIndex == $rightFirstTimeGrainIndex, $leftFirstTimeGrain.day == $rightFirstTimeGrain.day)

    then
        scoreHolder.penalize(kcontext);

end

rule "One TimeGrain Fiscal"
    when
        $left : AudienciaAssignment(startingTimeGrain != null,  $leftEnd : getLastTimeGrainIndex(), $leftFirstTimeGrain : startingTimeGrain, $fiscal : getFiscal())
        AudienciaAssignment(startingTimeGrain != null, $leftEnd == startingTimeGrain.getGrainIndex() - 1, $rightFirstTimeGrainIndex : startingTimeGrain.grainIndex, $rightFirstTimeGrain : startingTimeGrain, getFiscal() == $fiscal, isThereABreak($left) == false)
        TimeGrain(grainIndex == $rightFirstTimeGrainIndex, $leftFirstTimeGrain.day == $rightFirstTimeGrain.day)
    then
        scoreHolder.penalize(kcontext);

end



//rule "Distribute workload fairly"
//    when
//        $left : AudienciaAssignment($id : id, startingTimeGrain != null, $room : room)
//        $roomList : List() from collect(AudienciaAssignment(id != $id, room == $room))
//    then
//        scoreHolder.penalize(kcontext, ($roomList.size() * $roomList.size()));
//end



